# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: xduugu <xduugu@gmx.com>

# NOTE: Forked and updated from 12.0.0 to 14.0.0 since the
#       official package isn't being updated apparently

pkgbase=mkvtoolnix-n1klas
pkgname=('mkvtoolnix-n1klas-cli' 'mkvtoolnix-n1klas-gui')
pkgver=14.0.0
pkgrel=1
pkgdesc='Set of tools to create, edit and inspect Matroska files'
arch=('i686' 'x86_64')
url='https://www.bunkus.org/videotools/mkvtoolnix/'
license=('GPL')
makedepends=('boost' 'boost-libs' 'docbook-xsl' 'file' 'flac' 'libebml'
             'libmatroska' 'libogg' 'libvorbis' 'qt5-multimedia' 'ruby' 'zlib')
source=("https://mkvtoolnix.download/sources/mkvtoolnix-${pkgver}.tar.xz"
        "https://mkvtoolnix.download/sources/mkvtoolnix-${pkgver}.tar.xz.sig")
sha512sums=('636c8b149f61548abc53d3cfdbb0ab32cc88853ae23d010a35151ea1e12011e93a9350fc0b2d7a0367effb63d86bc6dffe26c2826df1f867bdb4f7939f81c295'
            'ee1c4f6f4b9a68cb94f416df5fa38fef90223e36549a4c6d638eb51e960f68095508bd948ab0c62be57fb9f38756cab002756a41e048ee57c5fc35df33cf335a')
validpgpkeys=('D9199745B0545F2E8197062B0F92290A445B9007')

build() {
  cd mkvtoolnix-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-qt
  rake apps:mkvinfo $MAKEFLAGS
  mv src/mkvinfo{,-cli}

  ./configure \
    --prefix='/usr'
  rake $MAKEFLAGS
}

package_mkvtoolnix-n1klas-cli() {
  depends=('boost-libs' 'file' 'flac' 'gcc-libs' 'glibc' 'libogg' 'zlib'
           'libebml.so' 'libmatroska.so' 'libvorbis.so')
  conflicts=('mkvtoolnix-cli')
  provides=('mkvtoolnix-cli')

  cd mkvtoolnix-${pkgver}

  rake DESTDIR="${pkgdir}" install
  install -m 755 src/mkvinfo-cli "${pkgdir}"/usr/bin/mkvinfo
  rm -rf "${pkgdir}"/usr/share/{applications,icons,mime,mkvtoolnix}
  find "${pkgdir}" -name mkvtoolnix-gui* -delete
}

package_mkvtoolnix-n1klas-gui() {
  depends=('desktop-file-utils' 'hicolor-icon-theme' 'mkvtoolnix-n1klas-cli'
           'qt5-multimedia' 'shared-mime-info' 'xdg-utils')
  replaces=('mkvtoolnix-gtk')
  conflicts=('mkvtoolnix-gui')
  provides=('mkvtoolnix-gui')

  cd mkvtoolnix-${pkgver}

  rake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/locale
  for t in mkv{extract,info,merge,propedit}; do
    find "${pkgdir}" -name $t* -delete
  done
}

# vim: ts=2 sw=2 et:
