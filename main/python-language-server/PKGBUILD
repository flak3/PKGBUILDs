# Maintainer: Niklas <dev@n1klas.net>

pkgbase=python-language-server
pkgname=(python-language-server python2-language-server)
_name=language-server
pkgver=0.14.0
pkgrel=1
pkgdesc="A Python implementation of the Language Server Protocol"
arch=('any')
url="https://github.com/palantir/python-language-server"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/palantir/python-language-server/archive/${pkgver}.tar.gz")
sha512sums=('1a31e8def0382cd61d3f4e69ba69f10a2663282ff0c2a6a17c4bb8b653efc2f75e3d741860692e21f4426c7934b3377785fdf987044c8dd50bb333f1c0629217')

prepare() {
	cp -a "${pkgbase}-${pkgver}"{,-python2}

    # Remove configparser dependency from python3 package because it does not
    # work on Arch and is not needed anyway since python3 is always >=3.5
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i -e "s#'configparser',##" setup.py

    # python2 package fixes:
    cd "${srcdir}/${pkgbase}-${pkgver}-python2"
    # Move the executable to pyls2 so it can be installed
    # alongside the python3 package
    sed -i -e "s#pyls = pyls.__main__:main#pyls2 = pyls.__main__:main#" setup.py
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${pkgbase}-${pkgver}-python2"
    python2 setup.py build
}

package_python-language-server() {
    depends=('python-future>=0.14.0'
             'python-jedi>=0.10'
             'python-json-rpc'
             'python-mccabe'
             'python-pluggy'
             'python-pycodestyle'
             'python-pydocstyle>=2.0.0'
             'python-pyflakes'
             'python-rope>=0.10.5'
             'yapf')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-language-server() {
    depends=('python2-configparser'
             'python2-future>=0.14.0'
             'python2-jedi>=0.10'
             'python2-json-rpc'
             'python2-mccabe'
             'python2-pluggy'
             'python2-pycodestyle'
             'python2-pydocstyle>=2.0.0'
             'python2-pyflakes'
             'python2-rope>=0.10.5'
             'python2-yapf')

    cd "${srcdir}/${pkgbase}-${pkgver}-python2"

    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
