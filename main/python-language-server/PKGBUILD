# Maintainer: Niklas <dev@n1klas.net>

pkgbase=python-language-server
pkgname=(python-language-server python2-language-server)
_name=language-server
pkgver=0.12.2
pkgrel=1
pkgdesc="A Python implementation of the Language Server Protocol"
arch=('any')
url="https://github.com/palantir/python-language-server"
license=('MIT')
makedepends=("python-setuptools" "python2-setuptools")
source=("https://github.com/palantir/python-language-server/archive/${pkgver}.tar.gz")
sha512sums=('eb2700758ade19dab530d3ea8b1f97b4e55da2f5d655162106a1631fe644507af308b2101738c7d787ad263f3fc1119c41d7dd3e9ac4f3d7e7e3c8520faef972')

prepare() {
	cp -a ${pkgbase}-${pkgver}{,-python2}

    # Remove configparser dependency from python3 package because it does not
    # work on Arch and is not needed anyway since python3 is always >=3.5
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i -e "s#'configparser',##" setup.py

    # python2 package fixes:
    cd "${srcdir}/${pkgbase}-${pkgver}-python2"
    # Temporarily change rope dependency because the Arch package is outdated
    sed -i -e "s#rope>=0.10.5#rope#" setup.py
    # Move the executable to pyls2 so it can be installed
    # alongside the python3 package
    sed -i -e "s#pyls = pyls.__main__:main#pyls2 = pyls.__main__:main#" setup.py
}

package_python-language-server() {
    depends=('python-future>=0.14.0'
             'python-jedi>=0.10'
             'python-json-rpc'
             'python-mccabe'
             'python-pycodestyle'
             'python-pydocstyle'
             'python-pyflakes'
             'python-rope>=0.10.5'
             'yapf'
             'python-pluggy')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-language-server() {
    depends=('python2-future>=0.14.0'
             'python2-jedi>=0.10'
             'python2-json-rpc'
             'python2-mccabe'
             'python2-pycodestyle'
             'python2-pydocstyle'
             'python2-pyflakes'
             'python2-rope' # setup.py specifies >=0.10.5 which is currently not available on arch
             'python2-yapf'
             'python2-pluggy'
             'python2-configparser')

    cd "${srcdir}/${pkgbase}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
