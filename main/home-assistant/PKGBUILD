# Maintainer: Gr√©goire Seux <grego_aur@familleseux.net>
# Contributor: Dean Galvin <deangalvin3@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgname="home-assistant"
pkgdesc='Open-source home automation platform running on Python 3'
pkgver=0.69.0
pkgrel=2
url="https://home-assistant.io/"
license=('Apache')
arch=('any')
replaces=('python-home-assistant')
makedepends=('python-setuptools')
# NB: this package will install additional python packages in /var/lib/hass/lib depending on components present in the configuration files.
depends=('python-aiohttp>=3.1.3'
         'python-aiohttp-cors>=0.5.3'
         'python-astral>=1.6.1'
         'python-async-timeout>=2.0.1'
         'python-attrs>=17.4.0'
         'python-certifi>=2017.4.17'
         'python-jinja>=2.10'
         'python-netifaces'
         'python-pip'
         'python-pytz>=2017.2'
         'python-requests>=2.18.4'
         'python-vincenty'
         'python-voluptuous>=0.11.1'
         'python-webcolors'
         'python-yaml>=3.11')
optdepends=('git: install component requirements from github'
            'net-tools: necessary for nmap discovery')
source=("https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        'home-assistant.service'
        'home-assistant.sysusers'
        'home-assistant-tmpfile.conf')
sha512sums=('6523c26b4bdaaf541009b5cc5f1c3206a27c98509f3acecfd523f2b87fe5fb673f6c9241e07a2a64ab0ea386f2e991a6fa9397f0eb3244d6237d72fa661b0d13'
            '63f6239c91ce79eccb44cf273ef77de433acf2c3c6ba3fe639f03295c7bf3d405ad3b392d779e31134dae12668ebe4ed74e20c3a20fa9372deffa0f94eb49667'
            '100665ac35370c3ccec65d73521568de21cebf9e46af364124778861c94e338e32ad9abb675d3917f97d351dd7867e3ab2e80c26616330ae7cf0d9dc3f13369b'
            '8babcf544c97ec5ad785014f0b0d5dca556a2f5157dadcbe83d49d4669b74f6349e274810ec9a028fcec208c6c8fbbe6b3899d2933b56163b9e506570879a3ad')
install='hass.install'

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  replace '==' '>=' setup.py
  # typing package is a backport of standard library < 3.5
  replace 'typing>=3,<4' '' setup.py
  replace 'attrs>=18.1.0' 'attrs>=17.4.0' setup.py
}

replace() {
  pattern=$1
  substitute=$2
  file=$3
  echo -n "Replacing '$pattern' by '$substitute' in $file..."
  if grep -q $pattern $file && sed -i "s/$pattern/$substitute/" $file; then
    echo "DONE"
  else
    echo "FAILED"
    depname=$(echo $pattern | sed 's/[>=<].*$//')
    echo Current line in $file:
    grep $depname $file
    exit 1
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1

  install -Dm644 "${srcdir}/home-assistant.service" "${pkgdir}/usr/lib/systemd/system/home-assistant.service"
  install -Dm644 "${srcdir}/home-assistant.sysusers" "${pkgdir}/usr/lib/sysusers.d/hass.conf"
  install -Dm644 "${srcdir}/home-assistant-tmpfile.conf" "${pkgdir}/usr/lib/tmpfiles.d/hass.conf"
}
