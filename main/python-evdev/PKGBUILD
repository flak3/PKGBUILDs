# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-evdev
pkgname=(python-evdev python2-evdev)
_name=evdev
pkgver=0.7.0
pkgrel=1
pkgdesc="Python bindings for the linux input handling subsystem"
arch=('i686' 'x86_64')
url="https://github.com/gvalkov/python-evdev"
makedepends=('python-setuptools' 'python2-setuptools')
license=('BSD')
source=(https://github.com/gvalkov/python-evdev/archive/v${pkgver}.tar.gz)
sha512sums=('9757ce392d08f9de0446963cf5351a66dbc2373426bef26e63d71623df1f7c47d90f6e454649076ded214878e67f321380646da3f76bc0fb57f7eb9bbecd3e7a')

prepare() {
    cp -a ${pkgbase}-${pkgver}{,-python2}
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	python setup.py build

	cd "${srcdir}/${pkgbase}-${pkgver}-python2"
	python2 setup.py build
}

package_python-evdev() {
    depends+=('python')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	python setup.py install --skip-build -O1 --root="${pkgdir}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-evdev() {
    depends+=('python2')

	cd "${srcdir}/${pkgbase}-${pkgver}-python2"
	python2 setup.py install --skip-build -O1 --root="${pkgdir}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
