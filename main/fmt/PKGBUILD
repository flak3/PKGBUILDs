# Maintainer: Mihai Bi≈üog <mihai.bisog at [gmail] d0t com>

pkgname=fmt
pkgver=4.0.0
pkgrel=2
pkgdesc="Open-source formatting library for C++."
arch=("i686" "x86_64")
url="http://fmtlib.net"
license=("BSD")
makedepends=("cmake")
source=("https://github.com/fmtlib/fmt/archive/$pkgver.tar.gz"
        '0001-Fix-a-segfault-in-test-on-glibc-2.26-551.patch'
        '0002-Fix-a-segfault-in-test-on-glibc-2.26-551-take-2.patch')
sha512sums=('8b9f7ce4720c3caef6de4a75b8d4b0fd7db4f1638edca98d5ea95f4a5157aef8faefbac68438236691bd373111ca089b4d3864f7352f3fba1fe44392e9644f8b'
            'd7d7d769facbb389ac6a44d8bfd1a22befcea4eda2a1f08efb3a15639a4f8b39c0424a117924dec870b6a310446074b098e8247baaf63af9afda1fb7a2ab074e'
            '47af0da78e65f0d23b1748108ab846ba917fabc4c7f76be1fc4f1e1a03c3c480f5f97ea68e8814c03b7cffbe21c1734365cc1e562f8fd22de0d33c8cf85e9eef')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -p1 -i "${srcdir}/0001-Fix-a-segfault-in-test-on-glibc-2.26-551.patch"
    patch -p1 -i "${srcdir}/0002-Fix-a-segfault-in-test-on-glibc-2.26-551-take-2.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p build && cd build

    cmake -DCMAKE_BUILD_TYPE=Release  \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DFMT_DOC=OFF               \
          ..
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="${pkgdir}" install
    install -D -m644 ../LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D -m644 ../ChangeLog.rst "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog.rst"
}
