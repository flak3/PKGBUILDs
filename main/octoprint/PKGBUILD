# Maintainer: Yunhui Fu <yhfudev@gmail.com>
# Contributor: Doug Richardson <dougie.richardson@gmail.com>
# Contributor: feilen <feilen1000@gmail.com>
# Contributor: Thermionix <thermionix@gmail.com>

pkgname=octoprint
pkgver=1.3.4
pkgrel=5
pkgdesc="Responsive web interface for controlling a 3D printer (RepRap, Ultimaker, ...)"
arch=(any)
url="https://github.com/foosel/OctoPrint"
license=('AGPL3')
depends=(
    'python2-flask'
    'python2-tornado'
    'python2-sockjs-tornado'
    'python2-yaml'
    'python2-flask-login'
    'python2-flask-principal'
    'python2-flask-babel'
    'python2-flask-assets'
    'python2-markdown'
    'python2-pyserial'
    'python2-netaddr'
    'python2-watchdog'
    'python2-sarge'
    'python2-netifaces'
    'python2-pylru'
    'python2-rsa'
    'python2-pkginfo'
    'python2-requests'
    'python2-semanticversion'
    'python2-psutil'
    'python2-click'
    'python2-awesome-slugify'
    'python2-feedparser'
    'python2-chainmap'
    'python2-future'
    'python2-scandir'
    'python2-websocket-client'
    'python2-dateutil'
    'python2-blinker'
)
optdepends=(
    'ffmpeg: timelapse support'
    'mjpg-streamer: stream images from webcam'
    'v4l-mjpg-stream: stream images from a Video4Linux capable camera'
)
provides=('octoprint')
install="octoprint.install"
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/foosel/OctoPrint/archive/${pkgver}.tar.gz"
    octoprint.run
    octoprint.service
    octoprint-tornado401.patch
)
sha512sums=('df22247796b46f270f7525777194e983a12be3c5f719ffc4ed864d6012d2050b813df9e8f9ee95bdc7687838c96f7c102327a7b802d42abd84481ddd77c8ebea'
            '5c22c76e4089958ff42e2627f29c360fa0f9a73b849f1fe5092cdd9ae800323263b75ac7e23d7189badac9baa7daa850183bf2491680a0ac01c605531728da62'
            '8aca601edfd2aacae437bd6ed4548c6606d37cae61012c64219385e11d26c47c44b5d2c1ec98483c6609a3cc885e45d9b48499afcc66cd55b3da4e25ebfaf3db'
            '055c67179605d87c83702e2b2a97a48f630bd4673453ffd9bd9a84e0db9166ae4d0a193740cefc6d80ef17742dece7b378055520d9a8445895b40cd674fa1cd3')


prepare() {
    cd ${srcdir}/OctoPrint-${pkgver}

    # check the version of python2-tornado required by the OctoPrint
    # if it not python2-tornado 4.0.1, we need to switch back to new version!
    # If use the latest tornado, you don't need to patch the octoprint.
    VER=$(grep -Hrn '"tornado==' setup.py | awk -F= '{print $3}' | awk -F\" '{print $1}')
    VAL=$(echo $VER | awk -F. '{print 10000 * $1 + 100 * $2 + $3}')
    if (( $VAL <= 40001 )) ; then
        patch -p1 < "$srcdir/octoprint-tornado401.patch"
    else
        echo "The required version of tornado is now changed." 1>&2
    fi
}

package() {
    #cd ${srcdir}/${pkgname}
    cd ${srcdir}/OctoPrint-${pkgver}

    python2 setup.py install --root="$pkgdir/" --optimize=1

    install -D -m755 ${srcdir}/octoprint.run ${pkgdir}/usr/bin/octoprint
    install -D -m644 ${srcdir}/octoprint.service ${pkgdir}/usr/lib/systemd/system/octoprint.service
}
