# Maintainer: Gregoire Seux <grego_aur@familleseux.net>

pkgbase='python-astral'
_pkgbase='astral'
pkgname=('python-astral' 'python2-astral')
pkgver=1.6.1
pkgrel=1
arch=('any')
pkgdesc="Calculations for the position of the sun and moon."
url="https://github.com/sffjunkie/astral"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/sffjunkie/${_pkgbase}/archive/${pkgver}.tar.gz")
sha512sums=('c987e642224b503b334716a670e85100dd7dcab7c00105be9c2816653dc4c143869a05ff84dd3e1d3d1ab1e968217464f08a759e27e5061bd994a49ffb081e1f')

prepare() {
    cd "${srcdir}"

    cp -r "${_pkgbase}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pkgbase}-${pkgver}-python2"
    python2 setup.py build
}

package_python-astral() {
    depends=('python-pytz')

    cd "${srcdir}/${_pkgbase}-${pkgver}"

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-astral() {
    depends=('python2-pytz')

    cd "${srcdir}/${_pkgbase}-${pkgver}-python2"

    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
