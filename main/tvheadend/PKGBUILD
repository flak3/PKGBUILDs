# To enable hdhomerun, add 'libhdhomerun' to depends and '--enable-hdhomerun_client' to
# the configure command in build().

pkgname=tvheadend
pkgver=4.2.6
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://tvheadend.org/"
license=('GPL3')
depends=('avahi' 'openssl' 'uriparser' 'ffmpeg' 'pcre2' 'libfdk-aac')
makedepends=('git' 'python')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('tvheadend' 'hts-tvheadend' 'hts-tvheadend-svn' 'tvheadend-git')
backup=('etc/conf.d/tvheadend')

source=("https://github.com/tvheadend/tvheadend/archive/v${pkgver}.tar.gz"
        'tvheadend.sysusers'
        'tvheadend.tmpfiles'
        'tvheadend.conf.d'
        'ffmpeg-35.patch')
sha512sums=('6291b0ba1d9af11d5295bf6804988835e746db2d3ebbd465a22e293a1108225c8c361762b78213c881cd15d7dedd16092f28a97c9e5b38f44920848bfbaf9709'
            'b32b3b448ea18365a005f8f8f9a7537ebfd2f2099c8bee3dcdd47169be15f7ab784894ac050df51ae1d467329d8b670d89f8d4e1a048c3859ecced64e28dff49'
            '8b0df92215e713ae88af7baba17ea08bdad82625a94603038bf122052ea298226515d23ebeacce7fd401bd462312f8d6c73b50218d07da0c70e11d7ae87ae91b'
            '17c9b98da3cca129e7a7b485256f72faba767c1b6eafe9b4b3ebd9d3b9b0cd9cda5f7f5000c03bb3e26bbef8050539adb968458fbe21954ddef8d6a4dc18ac0f'
            'c51cba147e7c6e4e48a675c488f80fc89043c028a1e62e96d2743cfc9213eadb7fa152b3829170e9b014b25e7d5bb18d608af892ddc4cce136fb28406a584d14')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -p1 -i "${srcdir}/ffmpeg-35.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr --mandir=/usr/share/man/man1 --release \
        --python=python3 \
        --enable-avahi \
        --enable-zlib \
        --disable-ffmpeg_static --enable-libav \
        --disable-libx264_static --enable-libx264 \
        --disable-libx265_static --enable-libx265 \
        --disable-libvpx_static --enable-libvpx \
        --disable-libtheora_static --enable-libtheora \
        --disable-libvorbis_static --enable-libvorbis \
        --disable-libfdkaac_static --enable-libfdkaac \
        --disable-libopus_static --enable-libopus \
        --disable-nvenc \
        --enable-vaapi \
        --enable-inotify \
        --enable-epoll \
        --disable-pcre --enable-pcre2 \
        --enable-uriparser \
        --enable-dvben50221 \
        --enable-dbus_1 \
        --disable-hdhomerun_static
    make CFLAGS_NO_WERROR=yes
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/rpm/tvheadend.service" "${pkgdir}/usr/lib/systemd/system/tvheadend.service"
    sed -i 's|/etc/sysconfig|/etc/conf.d|g' "$pkgdir/usr/lib/systemd/system/tvheadend.service"

    install -Dm644 "${srcdir}/tvheadend.conf.d" "${pkgdir}/etc/conf.d/tvheadend"
    install -Dm644 "${srcdir}/tvheadend.sysusers" "${pkgdir}/usr/lib/sysusers.d/tvheadend.conf"
    install -Dm644 "${srcdir}/tvheadend.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/tvheadend.conf"
}

