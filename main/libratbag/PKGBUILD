# Maintainer: Carlos Silva <r3pek@r3pek.org>

pkgname=libratbag
pkgver=0.9.900
pkgrel=5
pkgdesc='A library to configure gaming mice'
arch=('i686' 'x86_64')
url='https://github.com/libratbag/libratbag'
license=('MIT')
depends=('python' 'libevdev' 'libudev.so')
makedepends=('systemd' 'meson' 'git')
source=("https://github.com/libratbag/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('95277e97afcaa393e05b80797681384842af6f803b8e51d49de6669498779c95')
conflicts=('libratbag-git' 'ratbagd<=0.4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    meson builddir --prefix=/usr/ -Denable-tests=false
    ninja -C builddir
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    DESTDIR="${pkgdir}" ninja -C builddir install

    install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
    install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
