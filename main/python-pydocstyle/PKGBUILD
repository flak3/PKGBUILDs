# Maintainer: Niklas <dev@n1klas.net>

pkgbase=python-pydocstyle
pkgname=(python-pydocstyle python2-pydocstyle)
_name=pydocstyle
pkgver=2.1.1
pkgrel=1
pkgdesc="tool for checking compliance with Python docstring conventions"
arch=('any')
url="https://github.com/PyCQA/pydocstyle"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha512sums=('f23939e0350094c67565613bab4ddf285dda5a7379e6ac2493953d6e60ab75588143fd374715765d2aa0728e7117d02e91312dbbe5a9cad28aa8e8e7f68e3bc4')

prepare() {
    cp -a ${_name}-${pkgver}{,-python2}

    cd "${srcdir}/${_name}-${pkgver}-python2"
    # Put the executable of the Python 2 version in a different location
    # to allow installing both versions at the same time
    sed -i -e "s#pydocstyle = pydocstyle.cli:main#pydocstyle2 = pydocstyle.cli:main#" setup.py
}

package_python-pydocstyle() {
    depends=('python-snowballstemmer' 'python-six')

    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pydocstyle() {
    depends=('python2-snowballstemmer' 'python2-six' 'python2-configparser')

    cd "${srcdir}/${_name}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
