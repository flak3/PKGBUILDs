# Maintainer: Niklas <dev@n1klas.net>

pkgbase=python-adb
pkgname=(python-adb python2-adb)
_name=adb
pkgver=1.2.0
pkgrel=3
_commit=bed1a53d9574b6afbd63d783ed142371b0d1e584
pkgdesc="A pure Python implementation of the Android ADB and Fastboot protocols"
arch=('any')
url="https://github.com/google/python-adb"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/google/python-adb/archive/${_commit}.tar.gz")
sha512sums=('a97ce7e650c3dafe12a2bd0558ec71ea9cea24a2f9f8224da290da17474001519de8fa55c5bf794ae5365c575f8ea0eefd21fd69ee8ce2926c8954cb386c9935')

prepare() {
    cd "${srcdir}"
	cp -a ${pkgbase}-${_commit}{,-python2}
    find "${pkgbase}-${_commit}-python2" -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

build() {
    cd "${srcdir}/${pkgbase}-${_commit}"
    python setup.py build

    cd "${srcdir}/${pkgbase}-${_commit}-python2"
    python2 setup.py build
}

package_python-adb() {
    depends=('python-libusb1' 'python-rsa')
    optdepends=('python-progressbar: progressbar for Fastboot')

    cd "${srcdir}/${pkgbase}-${_commit}"
	python setup.py install --skip-build  --root="${pkgdir}" --optimize=1
}

package_python2-adb() {
    depends=('python2-libusb1' 'python2-m2crypto')
    optdepends=('python2-progressbar: progressbar for Fastboot')

    cd "${srcdir}/${pkgbase}-${_commit}-python2"
	python2 setup.py install --skip-build  --root="${pkgdir}" --optimize=1
}
