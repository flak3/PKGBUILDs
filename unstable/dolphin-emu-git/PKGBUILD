# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lightning <sgsdxzy@gmail.com>

pkgbase=dolphin-emu-git
pkgname=('dolphin-emu-git' 'dolphin-emu-cli-git' 'dolphin-emu-qt-git')
pkgver=5.0.r5634.g5f0de43828
pkgrel=1
pkgdesc='A GameCube / Wii / Triforce emulator'
arch=('x86_64')
url='http://www.dolphin-emu.org/'
license=('GPL')
depends=('bluez-libs' 'curl' 'enet' 'ffmpeg' 'libao' 'libevdev' 'mbedtls'
         'miniupnpc' 'openal' 'portaudio' 'soundtouch' 'xdg-utils' 'wxgtk')
makedepends=('cmake' 'git' 'qt5-base')
optdepends=('pulseaudio: PulseAudio backend')
options=('!emptydirs')
source=('gitsrc::git+https://github.com/dolphin-emu/dolphin.git')
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/gitsrc"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/gitsrc"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "${srcdir}/gitsrc/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_QT2='TRUE' \
    -DUSE_SHARED_ENET='TRUE' \
    -DDISTRIBUTOR='repo.n1klas.net'
  make
}

package_dolphin-emu-git() {
  provides=('dolphin-emu')
  conflicts=('dolphin-emu')

  cd "${srcdir}/gitsrc/build"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin/dolphin-emu-{nogui,qt2}

  install -Dm 644 ../Data/51-usb-device.rules -t "${pkgdir}/usr/lib/udev/rules.d/"
}

package_dolphin-emu-cli-git() {
  depends=('dolphin-emu-git')

  cd "${srcdir}/gitsrc/build"

  install -dm 755 "${pkgdir}/usr/bin"
  install -m 755 Binaries/dolphin-emu-nogui "${pkgdir}/usr/bin/dolphin-emu-cli"
}

package_dolphin-emu-qt-git() {
  depends=('dolphin-emu-git' 'qt5-base')

  cd "${srcdir}/gitsrc/build"

  install -dm 755 "${pkgdir}/usr/bin"
  install -m 755 Binaries/dolphin-emu-qt2 "${pkgdir}/usr/bin/"
}

# vim: ts=2 sw=2 et:
