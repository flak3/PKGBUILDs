# Maintainer : Andrew Crerar <andrew (dot) crerar at gmail>
# Contributor: Rob McCathie <korrode at gmail>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>
# Contributor: heaven <aheaven87 at gmail dot com>
# Contributor: graysky <graysky at archlinux dot us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: MacWolf <macwolf at archlinux dot de>

# NOTICE: Enabled Daala and AV1 support and added pacman hook from vlc-nightly.

_name=vlc
pkgname=vlc-git
pkgver=3.0.0.r15662.g50e1f0711f
pkgrel=1
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player (GIT Version)"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL3')
depends=('a52dec'
         'daala-git'
         'faad2'
         'ffmpeg'
         'libaom-git'
         'libdca'
         'libdvbpsi'
         'libdvdnav'
         'libfdk-aac'
         'libmad'
         'libmatroska'
         'libmpcdec'
         'libmpeg2'
         'libproxy'
         'libshout'
         'libtar'
         'libtiger'
         'libupnp'
         'libxinerama'
         'libxpm'
         'lua'
         'sdl_image'
         'wayland-protocols'
         'taglib'
         'zvbi'
         'libsecret'
         'libarchive'
         'qt5-x11extras')
makedepends=('aalib'
             'avahi'
             'flac'
             'git'
             'libavc1394'
             'libbluray'
             'libcaca'
             'libdc1394'
             'libdvdcss'
             'libgme'
             'libgoom2'
             'libmicrodns'
             'libmtp'
             'libnotify'
             'librsvg'
             'libssh2'
             'lirc'
             'live-media'
             'opus'
             'portaudio'
             'projectm'
             'protobuf'
             'smbclient'
             'twolame'
             'vcdimager'
             'xosd'
             'qt5-svg')
optdepends=('aalib: for ASCII art plugin'
            'atk-git: for libnotify plugin'
            'avahi: for service discovery using bonjour protocol'
            'flac: for Free Lossless Audio Codec plugin'
            'gst-plugins-base-libs: for libgst plugins'
            'gtk2: for libnotify plugin'
            'kdelibs: KDE Solid hardware integration'
            'kwallet: for password storage'
            'libavc1394: for devices using the 1394ta AV/C'
            'libbluray: for Blu-Ray support'
            'libcaca: for colored ASCII art video output'
            'libcddb: for libcdda plugin'
            'libcdio: for audio CD playback support'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libgme: for libgme plugin'
            'libgoom2: for libgoom plugin'
            'libmicrodns: for Chromecast support'
            'libmtp: for MTP devices support'
            'libnotify: for notification plugin'
            'librsvg: for SVG plugin'
            'libssh2: for sftp support'
            'libva-intel-driver: back-end for intel cards'
            'libva-vdpau-driver: vdpau back-end for nvidia'
            'lirc: for lirc plugin'
            'lua-socket: for http interface'
            'ncurses: for ncurses interface support'
            'opus: for opus support'
            'portaudio: for portaudio support'
            'projectm: for ProjectM visualisation plugin'
            'protobuf: for Chromecast support'
            'smbclient: for SMB access plugin'
            'ttf-dejavu: for subtitle font'
            'ttf-freefont: for subtitle font'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'vcdimager: navigate VCD with libvcdinfo')
conflicts=("${_name}" 'vlc-dev' 'vlc-plugin' 'vlc-stable-git' 'vlc-nightly')
provides=("${_name}=${pkgver}")
options=('!emptydirs')
source=('gitsrc::git://git.videolan.org/vlc.git'
        'lua53_compat.patch'
        'update-vlc-plugin-cache.hook'
        'fixaom.patch')
sha512sums=('SKIP'
            '33cda373aa1fb3ee19a78748e2687f2b93c8662c9fda62ecd122a2e649df8edaceb54dda3991bc38c80737945a143a9e65baa2743a483bb737bb94cd590dc25f'
            '2f1015af384559bf4868bb989c06a7d281a8e32afb175ef397dbf1671bae3540a3a6b073a74ed77ed82e79a81f964a5a58a98c2a3f1b5e5cd5e9ea60d58c737f'
            '49c7807e5130bf409aa3a1669c241088a74498190e3343adf388ec4f475d4fdc0d3e29e14326de36a62ff03fe1c8dba419e350274fe936cf3f211aa3d5f209b1')

pkgver() {
  cd "${srcdir}"/gitsrc
  printf "%s.r%s.g%s\n" "$(grep 'AC_INIT' configure.ac | sed 's/[^0-9\.]*//g')" "$(git describe --tags --long | cut -d '-' -f 3)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/gitsrc

  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
  patch -p1 -i "${srcdir}/lua53_compat.patch"
  patch -p1 -i "${srcdir}/fixaom.patch"
}

build() {
  cd "${srcdir}"/gitsrc

  ./bootstrap

  CFLAGS+=" -I/usr/include/samba-4.0" CPPFLAGS+=" -I/usr/include/samba-4.0" \
  ./configure --disable-rpath \
              --enable-aa \
              --enable-aom \
              --enable-archive \
              --enable-avahi \
              --enable-bluray \
              --enable-chromecast \
              --enable-daala \
              --enable-faad \
              --enable-fdkaac \
              --enable-lirc \
              --enable-microdns \
              --enable-ncurses \
              --enable-nls \
              --enable-opus \
              --enable-realrtsp \
              --enable-sftp \
              --enable-upnp \
              --enable-vcdx \
              --enable-wayland \
              --prefix=/usr \
              --sysconfdir=/etc \
              LUAC=/usr/bin/luac  LUA_LIBS="`pkg-config --libs lua`" \
              RCC=/usr/bin/rcc-qt5

  make
}

package() {
  cd "${srcdir}"/gitsrc

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
      install -D -m644 "${srcdir}/gitsrc/share/icons/${res}x${res}/vlc.png" \
                       "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done

  install -Dm644 "${srcdir}/update-vlc-plugin-cache.hook" "${pkgdir}/usr/share/libalpm/hooks/update-vlc-plugin-cache.hook"
}
