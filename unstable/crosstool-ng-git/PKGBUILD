# -*- shell-script -*-
# Maintainer: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: goodmen <goodmenzy@gmail.com>

pkgname=crosstool-ng-git
pkgver=1.23.0.r215.g42cb9694
pkgrel=1
pkgdesc="crosstool-NG aims at building toolchains."
arch=('i686' 'x86_64')
url="http://crosstool-ng.org/"
license=('GPL')
depends=('ncurses' 'make')
makedepends=('git' 'flex' 'bison' 'gperf' 'help2man')
provides=('crosstool-ng')
conflicts=('crosstool-ng')
source=('gitsrc::git://github.com/crosstool-ng/crosstool-ng')
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/gitsrc"

	git describe --long --tags | sed 's/^crosstool-ng-//;s/-rc/rc/;s/-/.r/;s/-/./'
}

build () {
	cd "${srcdir}/gitsrc"

	./bootstrap
	./configure --prefix=/usr
	make
}

package () {
	cd "${srcdir}/gitsrc"

	make DESTDIR="${pkgdir}" install
	install -Dm644 ct-ng.comp "$pkgdir/usr/share/bash-completion/completions/ct-ng"
}
