# Maintainer: Lex Black <autumn-wind at web dot de>
# Contributor: Who uploaded it to AUR3

# NOTICE: Updated, removed unneccessary parts

_gitname=daala
pkgname="${_gitname}-git"
pkgver=0.0.r1696.g7278368
pkgrel=1
pkgdesc="next-next-gen video compression technology from Xiph.org, Mozilla and others"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://www.xiph.org/daala/"
license=('BSD')
depends=('glibc')
makedepends=('git' 'check' 'sdl2' 'wxgtk2' 'webkitgtk2' 'libogg' 'libpng' 'libjpeg-turbo')
checkdepends=('check')
provides=('libdaala' $_gitname)
conflicts=('libdaala' $_gitname)
options=('!libtool')
source=("gitsrc::git+https://git.xiph.org/daala.git")
md5sums=('SKIP')


pkgver() {
  cd "${srcdir}"/gitsrc
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
  mkdir -p "${srcdir}"/"${_gitname}"-build
  cd "${srcdir}"/"${_gitname}"-build

  "${srcdir}"/gitsrc/autogen.sh
  "${srcdir}"/gitsrc/configure --prefix=/usr --enable-analyzer
  make
  make tools
}

check() {
  cd "${srcdir}"/"${_gitname}"-build
  make check
}

package() {
  cd "${srcdir}"/"${_gitname}"-build

  make DESTDIR="$pkgdir/" install
  #make DESTDIR="$pkgdir/" install-docs
  #make DESTDIR="$pkgdir/" install-exec
  install -Dm644 "${srcdir}"/gitsrc/COPYING "$pkgdir/usr/share/licenses/daala-git/COPYING"
}
