# Maintainer: Jonni Westphalen <jonny.westphalen@googlemail.com>
pkgname=aurman-git
pkgver=2.9.28.r0.g1247ebe
pkgrel=1
pkgdesc="aurman AUR helper"
arch=('x86_64')
url="https://github.com/polygamma/aurman"
license=('MIT')
depends=('python' 'expac' 'python-requests' 'pyalpm' 'pacman' 'sudo' 'git' 'python-regex')
source=('gitsrc::git+https://github.com/polygamma/aurman.git#branch=master')
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}"/gitsrc

    ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "${srcdir}"/gitsrc
    python setup.py build
}

package() {
    cd "${srcdir}"/gitsrc
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 bash.completion "${pkgdir}"/usr/share/bash-completion/completions/aurman
}
