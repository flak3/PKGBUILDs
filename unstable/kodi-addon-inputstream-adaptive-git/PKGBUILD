# Maintainer: kevku <kevku@gmx.com>
pkgname=kodi-addon-inputstream-adaptive-git
pkgver=r230.1656efc
pkgrel=1
pkgdesc="InputStream client for adaptive streams for Kodi 18"
arch=('x86_64' 'i686')
url="https://github.com/peak3d/inputstream.adaptive"
license=('GPL2')
depends=('kodi>=18.0' 'kodi-platform>=18.0')
makedepends=('git' 'cmake' 'kodi-dev>=18.0')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
# kodi 17 2.0
#source=('kodi-addon-inputstream-adaptive-git::git+https://github.com/peak3d/inputstream.adaptive.git#branch=Krypton')
# kodi 18
source=('gitsrc::git+https://github.com/peak3d/inputstream.adaptive.git')
md5sums=('SKIP')

pkgver() {
        cd "${srcdir}/gitsrc"
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
        cd "${srcdir}/gitsrc"
        cmake \
            -DCMAKE_INSTALL_PREFIX=/usr
        make
}

package() {
        cd "${srcdir}/gitsrc"
        make DESTDIR="${pkgdir}/" install
        install -Dm644 wvdecrypter/libssd_wv.so "$pkgdir/usr/lib/kodi/addons/inputstream.adaptive/lib/libssd_wv.so"
}
