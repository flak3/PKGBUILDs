# Maintainer: Niklas <dev@n1klas.net>
# Contributor: Francois Menning <f.menning@protonmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: graysky <graysky AT archlnux.us>
# Contributor: FSven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=nct6775-git
_pkgbase=nct6775
pkgname=(nct6775-git nct6775-dkms-git)
pkgver=r152.gb79b887
pkgrel=3
_extramodules=extramodules-4.13-ARCH
pkgdesc="New driver for Nuvoton NCT6775F, NCT6776F, NCT6779D"
arch=('i686' 'x86_64')
url="http://roeck-us.net/linux/drivers/"
license=("GPL")
makedepends=('git' 'linux' 'linux-headers>=4.13' 'linux-headers<4.14')
provides=('nct6775')
conflicts=('nct6775')
source=("gitsrc::git+https://github.com/groeck/nct6775.git"
        'dkms.conf.in')
sha512sums=('SKIP'
            '3dedb8b6892d8110a28ca44be6c7dc973b11906028cfb231eaa8ac34c2ace920684ba142df21a89bb591505d9e2bfd97b64cc310ae8a49eda612705bbe2b88bc')

pkgver() {
    cd "$srcdir/gitsrc"
    printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/gitsrc"

    _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
    make KERNEL_BUILD=/usr/lib/modules/"$_kernver/build" \
      KERNEL_MODULES=/usr/lib/modules/"$_kernver" modules
}

package_nct6775-git() {
    depends=('linux>=4.13' 'linux<4.14')
    install=$_pkgbase.install

    install -Dm644 "$srcdir/gitsrc/$_pkgbase.ko" \
    "$pkgdir/usr/lib/modules/$_extramodules/$_pkgbase.ko"

    gzip -9 "$pkgdir/usr/lib/modules/$_extramodules/"*.ko
}

package_nct6775-dkms-git() {
    depends=('dkms')
    conflicts+=('nct6775-dkms' 'nct6775-git')

    cd "$srcdir/gitsrc"

    install -dm755 $pkgdir/usr/src/$_pkgbase-$pkgver
    cp -a LICENSE Makefile README* *.h *.c $pkgdir/usr/src/$_pkgbase-$pkgver/

    sed -e "s/@PACKAGE_VERSION@/$pkgver/" \
        -e "s/@PACKAGE_NAME@/$_pkgbase/" \
        $srcdir/dkms.conf.in > \
        $pkgdir/usr/src/$_pkgbase-$pkgver/dkms.conf
}
